configfile: "workflow/config/pipeline.yaml"
rawdata = Path(config["directories"]["rawdata"])
procdata = Path(config["directories"]["procdata"])
metadata = Path(config["directories"]["metadata"])
results = Path(config["directories"]["results"])
logs = Path(config["directories"]["logs"])
version = config["GDSC_version"]
release = config["GDSC_release"]


include: "rules/metadata.smk"
include: "rules/treatmentResponse.smk"
include: "rules/rnaseq.smk"
include: "rules/cnv.smk"
include: "rules/mutation.smk"
include: "rules/fusion.smk"

rule build_Pharmacoset:
    input:
        treatmentResponseExperiment = procdata / f"treatmentResponse/{version}_{release}_treatmentResponse_preprocessed.RDS",
        annotated_treatmentMetadata = procdata / metadata / f"{version}_{release}_treatmentMetadata_annotated.tsv",
        annotated_sampleMetadata = procdata / metadata / f"{version}_{release}_sampleMetadata_mappedCellosaurus.tsv",
        geneAnnotation = procdata / metadata / "preprocessed_geneAnnotation.tsv",
        summarizedExperimentLists = [
            rules.make_RNASEQ_SE.output.rse_list,
            rules.make_CNV_SE.output.rse_list,
            rules.make_MUTATION_SE.output.rse_list,
            rules.make_FUSION_SE.output.rse_list
],
    output:
        results / "data/pharmacoset.RDS",
    conda: "envs/snakemake.yaml"
    script:
        "scripts/build_PharmacoSet.R"
